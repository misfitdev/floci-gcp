# NOTE: Keep resource definitions in sync with ../compat-opentofu/main.tf

# ── GCS Bucket ────────────────────────────────────────────────────────────────
resource "google_storage_bucket" "compat" {
  name          = "floci-compat-bucket"
  location      = "US"
  force_destroy = true

  uniform_bucket_level_access = false

  labels = {
    env = "compat-test"
  }
}

resource "google_storage_bucket_object" "readme" {
  bucket       = google_storage_bucket.compat.name
  name         = "README.txt"
  content      = "floci-gcp terraform compat test"
  content_type = "text/plain"
}

resource "google_storage_bucket_object" "cloud_run_index" {
  bucket       = google_storage_bucket.compat.name
  name         = "index.html"
  content      = "floci-gcp terraform cloud run gcs volume"
  content_type = "text/html"
}

# ── IAM Service Account ───────────────────────────────────────────────────────
resource "google_service_account" "compat" {
  account_id   = "floci-compat-sa"
  display_name = "floci compat test service account"
}

# ── Secret Manager ────────────────────────────────────────────────────────────
resource "google_secret_manager_secret" "compat" {
  secret_id = "floci-compat-secret"
  project   = var.project

  replication {
    auto {}
  }
}

resource "google_secret_manager_secret_version" "compat" {
  secret      = google_secret_manager_secret.compat.id
  secret_data = "floci-gcp-compat-test-secret-value"
}

resource "google_secret_manager_secret_iam_member" "accessor" {
  project   = var.project
  secret_id = google_secret_manager_secret.compat.secret_id
  role      = "roles/secretmanager.secretAccessor"
  member    = "serviceAccount:${google_service_account.compat.email}"
}

resource "google_project_iam_member" "publisher" {
  project = var.project
  role    = "roles/pubsub.publisher"
  member  = "serviceAccount:${google_service_account.compat.email}"
}

# ── Cloud Run ────────────────────────────────────────────────────────────────
resource "terraform_data" "cloud_run_replacement" {
  input = var.cloud_run_replace_token
}

resource "google_cloud_run_v2_service" "compat" {
  name                = var.cloud_run_name
  location            = var.region
  deletion_protection = false
  ingress             = "INGRESS_TRAFFIC_ALL"

  labels = {
    env = var.cloud_run_label
  }

  template {
    service_account                  = google_service_account.compat.email
    timeout                          = "30s"
    max_instance_request_concurrency = 8

    scaling {
      min_instance_count = 0
      max_instance_count = 1
    }

    volumes {
      name = "site"

      gcs {
        bucket    = google_storage_bucket.compat.name
        read_only = true
      }
    }

    containers {
      image = "nginx:latest"

      ports {
        container_port = 80
      }

      env {
        name  = "FLOCI_COMPAT"
        value = var.cloud_run_env_value
      }

      volume_mounts {
        name       = "site"
        mount_path = "/usr/share/nginx/html"
      }

      resources {
        limits = {
          cpu    = "1"
          memory = "128Mi"
        }
        cpu_idle          = true
        startup_cpu_boost = true
      }
    }
  }

  lifecycle {
    replace_triggered_by = [
      terraform_data.cloud_run_replacement
    ]
  }

  depends_on = [
    google_storage_bucket_object.cloud_run_index
  ]
}

# ── Cloud SQL for PostgreSQL ─────────────────────────────────────────────────
resource "google_sql_database_instance" "compat" {
  name             = "floci-compat-postgres"
  project          = var.project
  region           = var.region
  database_version = "POSTGRES_18"

  deletion_protection = false

  settings {
    tier = "db-custom-1-3840"
  }
}

resource "google_sql_database" "compat" {
  name     = "appdb"
  project  = var.project
  instance = google_sql_database_instance.compat.name
}

resource "google_sql_user" "compat" {
  name     = "app"
  project  = var.project
  instance = google_sql_database_instance.compat.name
  password = "floci-compat-password"
}

# ── Cloud KMS ─────────────────────────────────────────────────────────────────
resource "google_kms_key_ring" "compat" {
  name     = "floci-compat-keyring"
  location = var.region
  project  = var.project
}

resource "google_kms_crypto_key" "compat" {
  name     = "floci-compat-key"
  key_ring = google_kms_key_ring.compat.id
  purpose  = "ENCRYPT_DECRYPT"
}

# ── Pub/Sub ───────────────────────────────────────────────────────────────────
resource "google_pubsub_topic" "compat" {
  name = "floci-compat-topic"

  labels = {
    env = "compat-test"
  }
}

resource "google_pubsub_subscription" "compat" {
  name  = "floci-compat-sub"
  topic = google_pubsub_topic.compat.id

  ack_deadline_seconds = 20
}

# Two members on one topic exercise the getIamPolicy → merge → setIamPolicy
# read-modify-write flow; both grants must survive.
resource "google_pubsub_topic_iam_member" "publisher" {
  topic  = google_pubsub_topic.compat.name
  role   = "roles/pubsub.publisher"
  member = "serviceAccount:${google_service_account.compat.email}"
}

resource "google_pubsub_topic_iam_member" "viewer" {
  topic  = google_pubsub_topic.compat.name
  role   = "roles/pubsub.viewer"
  member = "user:compat-viewer@example.com"
}

resource "google_pubsub_subscription_iam_member" "subscriber" {
  subscription = google_pubsub_subscription.compat.name
  role         = "roles/pubsub.subscriber"
  member       = "serviceAccount:${google_service_account.compat.email}"
}

# ── Outputs ───────────────────────────────────────────────────────────────────
output "key_ring_name" {
  value = google_kms_key_ring.compat.name
}

output "pubsub_topic_name" {
  value = google_pubsub_topic.compat.name
}

output "pubsub_subscription_name" {
  value = google_pubsub_subscription.compat.name
}

output "crypto_key_name" {
  value = google_kms_crypto_key.compat.name
}

output "bucket_name" {
  value = google_storage_bucket.compat.name
}

output "object_name" {
  value = google_storage_bucket_object.readme.name
}

output "service_account_email" {
  value = google_service_account.compat.email
}

output "secret_name" {
  value = google_secret_manager_secret.compat.name
}

output "secret_version_name" {
  value = google_secret_manager_secret_version.compat.name
}

output "cloud_run_service_name" {
  value = google_cloud_run_v2_service.compat.name
}

output "cloud_run_uri" {
  value = google_cloud_run_v2_service.compat.uri
}

output "sql_instance_name" {
  value = google_sql_database_instance.compat.name
}

output "sql_database_name" {
  value = google_sql_database.compat.name
}

output "sql_user_name" {
  value = google_sql_user.compat.name
}

# ── Service Usage ─────────────────────────────────────────────────────────────
resource "google_project_service" "run_api" {
  project = var.project
  service = "run.googleapis.com"

  disable_on_destroy = true
}

output "enabled_service" {
  value = google_project_service.run_api.service
}

# BigQuery: dataset defaults and table partitioning/clustering must round-trip, or the
# provider plans a change on every run.
resource "google_bigquery_dataset" "compat" {
  dataset_id                      = "floci_compat"
  friendly_name                   = "floci compat"
  description                     = "Terraform compat dataset"
  location                        = "US"
  default_table_expiration_ms     = 7200000
  default_partition_expiration_ms = 86400000
  default_collation               = "und:ci"
  labels                          = { env = "compat" }
  delete_contents_on_destroy      = true
}

resource "google_bigquery_table" "events" {
  dataset_id               = google_bigquery_dataset.compat.dataset_id
  table_id                 = "events"
  deletion_protection      = false
  description              = "partitioned and clustered"
  labels                   = { team = "data" }
  require_partition_filter = true
  clustering               = ["user_id", "kind"]

  time_partitioning {
    type  = "DAY"
    field = "occurred_at"
  }

  schema = jsonencode([
    { name = "occurred_at", type = "TIMESTAMP", mode = "REQUIRED" },
    { name = "user_id", type = "STRING", mode = "NULLABLE" },
    { name = "kind", type = "STRING", mode = "NULLABLE" }
  ])
}
